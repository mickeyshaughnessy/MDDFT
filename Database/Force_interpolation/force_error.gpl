set macros
set term x11
set encoding iso_8859_1
ps = "post eps color 22 lw 2 enhanced"

set linetype 1 lt 1 lw 2 lc rgbcolor "red"
set linetype 2 lt 1 lw 2 lc rgbcolor "orange"
set linetype 3 lt 1 lw 2 lc rgbcolor "gold"
set linetype 4 lt 1 lw 2 lc rgbcolor "web-green"
set linetype 5 lt 1 lw 2 lc rgbcolor "cyan"
set linetype 6 lt 1 lw 2 lc rgbcolor "blue"
set linetype 7 lt 1 lw 2 lc rgbcolor "violet"
set linetype cycle 7

ntrials = 20

unset key
set xlabel "RADIUS [{\305}]"
set ylabel "NUMBER"
data = '"radial_distribution.dat"'
file = '"radial_distribution.eps"'
plot for [i=0:ntrials-1] \
@data index i w l
pause -1 @file
set term @ps; set output @file; repl; set term x11; set output
!epstopdf @file ; rm -f @file

#plot 1/(1+x*x) w l
#pause -1 "rbf"

unset key
set xlabel "RADIUS [{\305}]"
set ylabel "RELATIVE ERROR"
file = '"force_error.eps"'
set logscale xy
plot for [i=0:ntrials-1] \
@data index i u 1:3 w lp lt (i+1)
pause -1 @file
set term @ps; set output @file; repl; set term x11; set output
!epstopdf @file ; rm -f @file

!cp -i force_error.pdf ../../Notes/Figures


